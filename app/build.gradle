buildscript {
    repositories {
        mavenCentral()
        google()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'
        classpath 'com.github.bjoernq:unmockplugin:0.7.9'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'de.mobilej.unmock'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId 'io.appium.uiautomator2'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 110
        archivesBaseName = 'appium-uiautomator2'
        /**
         * versionName should be updated and inline with version in package.json for every npm release.
         */
        versionName '5.7.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        debug {
            debuggable true
        }
        customDebuggableBuildType {
            debuggable true
        }
        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = outputFileName.replace('debug.apk', "v${defaultConfig.versionName}.apk")
            }
        }
    }
    flavorDimensions 'default'
    productFlavors {
        e2eTest {
            applicationId 'io.appium.uiautomator2.e2etest'
        }
        server {
            applicationId 'io.appium.uiautomator2.server'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/maven/com.google.guava/guava/pom.properties', 'META-INF/maven/com.google.guava/guava/pom.xml']
        }
    }
    lint {
        abortOnError false
    }
}

unMock {
    keepStartingWith 'com.android.internal.util.'
    keepStartingWith 'android.util.'
    keepStartingWith 'android.view.'
    keepStartingWith 'android.internal.'
}

dependencies {
    // https://download.eclipse.org/oomph/archive/reports/download.eclipse.org/releases/2021-09/index/org.eclipse.wst.xml.xpath2.processor_2.1.101.v201903222120.html
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    implementation 'androidx.test:core:1.4.0'
    implementation 'androidx.test:runner:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.9'
    //noinspection GradleDependency
    implementation 'commons-io:commons-io:2.6'
    implementation 'io.netty:netty-all:4.0.41.Final'
    implementation 'net.gcardone.junidecode:junidecode:0.4.1'
    // The next three dependencies are required for XPath search to work
    implementation 'xerces:xercesImpl:2.12.1'
    implementation 'com.github.vbmacher:java-cup-runtime:11b-20160615-1'
    //noinspection GradleDependency
    implementation 'com.ibm.icu:icu4j:60.2'
    unmock 'org.robolectric:android-all:7.1.0_r7-robolectric-0'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    //noinspection GradleDependency
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20190722'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
    testImplementation 'org.powermock:powermock-classloading-xstream:2.0.9'
    testImplementation 'org.powermock:powermock-module-junit4-rule:2.0.9'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.robolectric:robolectric:4.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'com.squareup.okhttp:okhttp:2.7.5'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.4.0'
    //noinspection GradleDependency
    androidTestImplementation 'junit:junit:4.12'
}

task installAUT(type: Exec) {
    group = 'test'
    description = 'Install app under test'
    doFirst {
        println('Install AUT')
        def adb = "$System.env.ANDROID_HOME/platform-tools/adb"
        def apk = file('../node_modules/android-apidemos/apks/ApiDemos-debug.apk')
        def sdkVersion = "$adb shell getprop ro.build.version.sdk".execute().text.trim()
        println "Device SDK version: $sdkVersion"
        /* This seems weird but it is what it is: https://github.com/wix/detox/issues/274 */
        def command = sdkVersion.toInteger() < 23 ? "$adb install -r $apk" : "$adb install -r -g $apk"
        println "Execute command: $command"
        commandLine command.split(' ')
    }
}

afterEvaluate {
    tasks.each { task ->
        if (task.name.startsWith('connectedE2eTestDebugAndroidTest')) {
            task.dependsOn installAUT
        }
    }
}
